plugins {
    id 'java'
    id 'maven-publish'
    id "org.sonarqube" version "3.0"
}

apply plugin: 'jacoco'

group 'den.vor'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

ext.versions = [
        apacheRng: '1.2',
        cache: '3.8.1',
        hamcrest: '2.2',
        junit : '5.7.0',
        mockitoJunit: '3.6.0',
        jackson: '2.11.3'
]

test {
    useJUnitPlatform()
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                    '**/*Configuration.class'
            ])
        })
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled true
    }
}
test.finalizedBy(jacocoTestReport)

dependencies {
    compile("org.apache.commons:commons-rng-core:${versions.apacheRng}")
    compile("org.ehcache:ehcache:${versions.cache}")
    compile("com.fasterxml.jackson.core:jackson-databind:${versions.jackson}")

    testCompile("org.junit.jupiter:junit-jupiter-api:${versions.junit}")
    testCompile("org.junit.jupiter:junit-jupiter-engine:${versions.junit}")
    testCompile("org.junit.jupiter:junit-jupiter-params:${versions.junit}")
    testCompile("org.mockito:mockito-junit-jupiter:${versions.mockitoJunit}")
    testCompile("org.hamcrest:hamcrest-core:${versions.hamcrest}")
}

sonarqube {
    properties {
        property "sonar.projectKey", "EasyObject_easy-object"
        property "sonar.organization", "easyobject"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

file("sonar-project.properties").withReader {
    Properties sonarProperties = new Properties();
    sonarProperties.load(it)

    sonarProperties.forEach { key, value ->
        sonarqube {
            properties {
                property key, value
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}